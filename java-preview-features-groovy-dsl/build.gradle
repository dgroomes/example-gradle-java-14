plugins {
    id 'java'
    id 'application'
}

repositories {
    jcenter()
}

def junitVersion = "5.7.0" // releases: https://junit.org/junit5/docs/current/release-notes/index.html

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

java {
    sourceCompatibility = JavaVersion.VERSION_15
    targetCompatibility = JavaVersion.VERSION_15
}

/**
 * Configure the compiler task, test task, start script creation task, and the run task to enable Java language "Preview
 * Features"
 */
tasks.withType(JavaCompile) {
    options.compilerArgs += ["--enable-preview"]
}
tasks.test {
    jvmArgs = ["--enable-preview"]
}
tasks.startScripts {
    defaultJvmOpts = ["--enable-preview"]
}
tasks.run {
    jvmArgs = ["--enable-preview"]
}

application {
    mainClassName = 'dgroomes.App'
}

test {
    useJUnitPlatform()
}
